
#Использовать ReadParams
#Использовать 1commands
#Использовать gui
#Использовать "."

Перем Источник;
Перем ПодготовленныйТекст;
Перем Настройки;

Процедура ПроверитьОрфографию()
	
	начало = ТекущаяУниверсальнаяДатаВМиллисекундах();

	ПодготовкаТекста = Новый ПодготовкаТекста;

	Если ЧтениеПараметров.Получить( "debug", Ложь ) Тогда
	
		ПодготовкаТекста.УстановитьРежимОтладки();

	КонецЕсли;

	Сообщить( "Источник: " + Источник );
	Сообщить( "Подготовленный текст: " + ПодготовленныйТекст );

	ПодготовкаТекста.Подготовить( Источник, ПодготовленныйТекст );
	файлРезультат = ПодготовкаТекста.ИмяПодготовленногоФайла + ".out";

	командаПроверки = "java -Dfile.encoding=UTF-8 -jar %1 -l ru -c UTF-8 --enablecategories MORFOLOGIK_RULE_RU_RU --line-by-line %2>%3";
	командаПроверки = СтрШаблон( командаПроверки,
		ЧтениеПараметров.Получить( "LanguageTool.path" ),
		ПодготовкаТекста.ИмяПодготовленногоФайла,
		файлРезультат
	   );

	Команда = Новый Команда;
	Команда.УстановитьСтрокуЗапуска(командаПроверки);

	КодВозврата = Команда.Исполнить();

	Сообщить( командаПроверки );
	Сообщить( "Код возврата: " + кодВозврата );
	Сообщить( Команда.ПолучитьВывод() );

	Попытка
		
		УдалитьФайлы( ПодготовкаТекста.ИмяПодготовленногоФайла );
		
	Исключение
	КонецПопытки;

	Сообщить( "Затрачено: " + (ТекущаяУниверсальнаяДатаВМиллисекундах() - начало) + "мс" );

	ПоказатьРезультат( файлРезультат );

	Попытка
		
		УдалитьФайлы( файлРезультат );
		
	Исключение
	КонецПопытки;

КонецПроцедуры

Процедура ПоказатьРезультат( пИмяФайлаРезультата )
	
	чтениеФайла = Новый ТекстовыйДокумент;
	чтениеФайла.Прочитать(пИмяФайлаРезультата, "UTF-8");
	текстФайла = чтениеФайла.ПолучитьТекст();

	УправляемыйИнтерфейс = Новый УправляемыйИнтерфейс();
	Форма = УправляемыйИнтерфейс.СоздатьФорму();

	ПолеТекстовогоДокумента = Форма.Элементы.Добавить("ПолеТекстовогоДокумента", "ПолеФормы", Неопределено);
	ПолеТекстовогоДокумента.Вид = Форма.ВидПоляФормы.ПолеТекстовогоДокумента;
	ПолеТекстовогоДокумента.Значение = текстФайла;
	ПолеТекстовогоДокумента.Высота = 300;

	Форма.Показать();

КонецПроцедуры

Процедура Выполнить(Параметры)

	ошибкиЧтения = Неопределено;

	файлыНастроек = Новый Массив;
	файлыНастроек.Добавить( ".\scripts\CheckOrfo\CheckOrfo.json" );
	файлыНастроек.Добавить( ".\settings\CheckOrfo.json" );

	Настройки = ЧтениеПараметров.Прочитать( файлыНастроек , ошибкиЧтения);

	Источник = "";
	Если Параметры.Количество() > 0 Тогда
		Источник = Параметры[0];
	КонецЕсли;

	ПодготовленныйТекст = "";
	Если Параметры.Количество() > 1 Тогда
		ПодготовленныйТекст = Параметры[1];
	КонецЕсли;

	ПроверитьОрфографию();
	
КонецПроцедуры

Выполнить(АргументыКоманднойСтроки);





Перем СоответствиеПоложений Экспорт;
Перем ИмяПодготовленногоФайла Экспорт;
Перем РежимОтладки;

Процедура Подготовить( Знач пИсточник = "", Знач пИмяПодготовленногоФайла = "" ) Экспорт
	
	ИсходныйТекст = Новый ТекстовыйДокумент;
	
	Если пИсточник = ""
		ИЛИ пИмяПодготовленногоФайла = "" Тогда
		
		Обмен = ЗагрузитьСценарий("core/Обмен.os");
		
	КонецЕсли;
	
	Если пИсточник = "" Тогда
		
		ИсходныйТекст.УстановитьТекст( Обмен.ПолучитьВыделенныйТекст() );
		
		Если РежимОтладки() Тогда
			
			ИсходныйТекст.Записать("tmp\in.txt");
			
		КонецЕсли;

	Иначе
		
		ИсходныйТекст.Прочитать( пИсточник, КодировкаТекста.ANSI);
		
	КонецЕсли;

	подготовленныйТекст = Новый ТекстовыйДокумент;
	
	ПодготовитьПоТабличномуДокументу( ИсходныйТекст, подготовленныйТекст);
	
КонецПроцедуры

Процедура ПодготовитьПоТабличномуДокументу( Знач ИсходныйТекст, подготовленныйТекст ) Экспорт
	
	СоответствиеПоложений = Новый Соответствие;
	
	Для цНомерСтроки = 1 По ИсходныйТекст.КоличествоСтрок() Цикл
		
		текСтрока = ИсходныйТекст.ПолучитьСтроку( цНомерСтроки );
		
		результат = ПодготовитьСтроку( текСтрока );
		
		подготовленныйТекст.ДобавитьСтроку( результат.Строка );
		
		СоответствиеПоложений.Вставить( цНомерСтроки, результат.Подмены );
		
	КонецЦикла;
	
КонецПроцедуры

Функция ПодготовитьСтроку( Знач пСтрока )
	
	подмены   = Новый Соответствие;
	новСтрока = "";
	
	позицияВНовойСтроке = 1;
	этоСлово            = Ложь;
	предСимволВРег      = Ложь;
	
	Для ц = 1 По СтрДлина( пСтрока ) Цикл
		
		текСимвол = Сред( пСтрока, ц, 1 );
		
		Если ЭтоБуква( текСимвол ) Тогда
			
			Если ВРег( текСимвол ) = текСимвол
				И этоСлово
				И Не предСимволВРег Тогда // Начало слова внутри строки
				
				новСтрока           = новСтрока + " ";
				позицияВНовойСтроке = позицияВНовойСтроке + 1;
				подмены.Вставить( позицияВНовойСтроке, ц );
				
			ИначеЕсли Не этоСлово Тогда // новое слово
				
				подмены.Вставить( позицияВНовойСтроке, ц );
				
			КонецЕсли;
			
			новСтрока           = новСтрока + текСимвол;
			позицияВНовойСтроке = позицияВНовойСтроке + 1;
			предСимволВРег      = ВРег( текСимвол ) = текСимвол;
			
			этоСлово = Истина;
			
		ИначеЕсли этоСлово Тогда // конец слова
			
			этоСлово            = Ложь;
			новСтрока           = новСтрока + " ";
			позицияВНовойСтроке = позицияВНовойСтроке + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Новый Структура( "Строка,Подмены", новСтрока, подмены );
	
КонецФункции

Функция ЭтоБуква( Знач пСимвол )
	
	всеБуквы = "абвгдеёжзийклмнопрстуфхцчшщъыьэюяabcdefghijklmnopqrstuvwxyz";
	
	Возврат СтрНайти( всеБуквы, НРег( пСимвол ) ) > 0;
	
КонецФункции

Функция РежимОтладки()
	
	Возврат РежимОтладки = Истина;

КонецФункции

Процедура УстановитьРежимОтладки()
	
	РежимОтладки = Истина;

КонецПроцедуры